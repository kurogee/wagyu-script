Get Started
---
「Get Started」では、この言語についてを知り、セットアップをして、簡単にこの言語を知っていきましょう。

この言語はなんですか？
---
この言語は、「Wagyu-script」という半角スペース区切りが特徴のプログラミング言語です。名前の由来は私のネット上でのユーザー名である「@kuroge」にちなんで名づけました。

この言語の始め方
---
リポジトリのページの左側に「Releases」という項目があるのでそこからそれぞれのOS向けの最新版をDLしてください。インストールは不要です。

DL後、ファイル解凍後の実行方法は以下の通りです。（拡張子は、``.wg``としていますが、テキストファイルなら``.txt``などでも大丈夫です。）

GNU/Linux
```sh
./wagyu-script run ファイル名.wg
```
Windows
```cmd
./wagyu-script.exe run ファイル名.wg
```

Hello, World!
---
この言語はかのC言語やJavaと比べて簡単に書くことができます。以下は``Hello, World!``とプロンプトに出力する一番基本的なプログラムです。
```
println "Hello, World!";
```
一般的な出力（行末に改行をつける）は``println``関数でできます。基本的に引数は半角スペース区切りで関数名のあとに続きます。引数の``"～"``は**文字列型**と言います。クオーテーションで囲まれたものです。シングルクオーテーションで``'～'``とも表せます。

また正規表現を利用する際には、文字列を`` `〜` ``（バッククォート）で囲むこともできます。

またこの言語は1関数ごとの行末に``;``（セミコロン）が必要です。関数ごとの改行あり/なしによってプログラムの意味が変わることがないので、一行に詰めて書くことも可能になります。
```
println "Hello!";println "World!";
```
※ ``println``の引数には変数も入れられます。

コメント
---
コメントというのは、プログラムに関するメモなどを自由に記述できるものです。コメントの内容は動作に影響を与えません。

```
// コメントの内容;
```
コメントにも行末に``;``が必要であることに注意してください。

型
---
この言語ではほとんど自動で解釈されるためほぼ使われませんが、型を指定する関数もあります。以下はその型の一覧です。

| 名称 | 型名 | 型に入れられる内容 |
|---|---|---|
| 文字列型 | string | 文字列（ex: ``"Hello!"``） |
| 配列型 | array | 配列（ex: ``("Hello" "World" "Wagyu")``） |
| 整数型 | int | 整数の値（ex: ``10``） |
| 小数型 | float | 小数の値（ex: ``3.14``）|
| 辞書型 | dict | 辞書（ex: ``{"key1" : "value1" "key2" : 2 ...}``） |
| 正規表現型 (予約) | regex | 正規表現（ex: ```[a-z]```） |

その他の出力
---
``println``以外にも以下のような出力の関数を用意しています。

 - ``printf`` フォーマットに則って文字列に変数を入れ込み出力する（行末の改行**無し**）。
    ```
    printf "フォーマット" 変数1 変数2 ...;
    ```
    フォーマットでは、例えば``:1:``を入れると、引数に追加された1番目の変数名をそこに入れ込むことができます。以下はそれを用いたプログラムの例です。

    ※ この例では、変数``x``に``Tanaka``という文字列 型の値が入っているものとします。
    ```
    printf "Hello, :1:!" x;
    // -> 「Hello, Tanaka!」と出力されます。
    ```
 - ``print`` 文末の改行無しで、文字列を出力する。
    ```
    print 文字列or変数名;
    ```

変数
---
変数を宣言・初期化には二通りの方法があります。

1.  変数を宣言し、初期化する
    - 普通の値の場合
        ```変数名 = value 値;```
    - 配列の場合
        ```変数名 = array (値1 値2 値3 ...);```
    - 省略することもできます
        ```変数名 = 値;```
    - 複数の変数を宣言する場合
        ```vars (変数名1 変数名2 ...) (値1 値2 ...);```
2. 空の変数を宣言のみする
    - 1つだけの場合
        ```make var 変数名;```
    - 2つ以上の場合
        ```make var 変数名1 変数名2 ...;```

※ 値はすべて文字列型として代入し、計算する都度数値型に戻されます。

※ 変数の参照をするときは引数に変数名を入れれば、自動的に参照先の値に戻されたりします。

変数の値を単に増やしたい/減らしたい時は、``add``というのも使うことができます。
```
変数名 = add 数値(負の数も対応);
```
以下はaddを使って変数``x``の値を+1するプログラムの例です。
```
x = value 1;
x = add 1;

println x;
// -> 「2」が出力される;
```

演算
---
変数の代入や関数の引数などに計算した結果などを入れたい場合は、``#(計算式)``という独自の機能である**シャープ関数**を使います。この関数について詳しくは[シャープ関数](./sharp-function.md)を参照してください。（これらを一通り読んだ後に読むのをおすすめします。）

例えば、変数``x``に``1 + 1``の演算結果を入れたい場合は以下のようにします。
```
x = value #(1 + 1);
```
変数名も入れることができます。演算子、値、変数名もそれぞれ半角スペースで区切る必要があります。
```
y = value #(x+1);
// エラーになる例。ちゃんと半角スペースで区切りましょう;
```

条件分岐
---
この言語での条件分岐（if文）は以下のように書きます。3通りの書き方があります。

1. 真の時に実行されるプログラムだけ書く場合
```
if 条件文 {
    実行内容;
    ...
};
```
2. 真の時と偽の時に実行されるプログラムを書く場合
```
if 条件文 {
    真の時の実行内容;
    ...
} else {
    偽の時の実行内容;
    ...
};
```
3. 真の時と偽の時、そして偽の時にもまた条件分岐（else-if）を設けるとき
```
if 条件文 {
    真の時の実行内容;
} elif 条件文2 {
    真の時の実行内容;
} else {
    偽の時の実行内容;
};
```

条件文は``true``と``false``、もしくは``1``（true）・``0``（false）という演算結果の値で判断します。また、条件文のところは先ほどの``#(～)``で書くことができます。

以下は、変数``x``が1であったら``Good!``と表示すし、そうでなかったら``Oh, no...``と出力するプログラムです。
```
if #(x == 1) {
    println "Good!";
} else {
    println "Oh, no...";
};
```

マッチ文
---
この言語では、「数字や文字の一致を確かめるだけなのに``if``じゃあ面倒くさいなぁ」ということで、``match``という機能を提供しています。``match``は、条件に合致するときに実行される内容を書くことができます。

``match``の書き方は以下の通りです。
```
match 変数名
case 値1 {
    実行内容;
    ...
}
case 値2 {
    実行内容;
    ...
}
default {
    実行内容;
    ...
};
```
``値``は変数も入れることができます。また、``case``の後には``{}``で囲んで実行内容を書きます。

``case``は、条件に合致する値を書きます。もし、その値が``変数名``の値と一致した場合、その``case``内の内容が実行されます。また、条件に合うケースが複数ある時はそれらがすべて実行されるため``break``などを適度使ってください。

もしシャープ関数を使ったら、その結果が``true``もしくは``1``の場合に実行されます。その場合、シャープ関数内引数の``変数名``にあたるところは``_``で代用できます。
```
x = value 1;

match x
case #(_ == 1) { // 実際この程度なら普通に1と書いた方がいいですが、例示のために;
    println "Good!";
}
case #(_ == 2) {
    println "Nice!";
};
```
このように``_``で書いた所は、変数``x``の値、つまり``1``に置き換えられます。

``default``は、どの``case``にも合致しなかったときに実行される内容を書きます。``match``文内の一番最後に書く必要があります。

以下は、変数``x``が1のとき``Good!``、2のとき``Nice!``、それ以外のとき``Oh, no...``と出力するプログラムの例です。
```
match x
case 1 {
    println "Good!";
}
case 2 {
    println "Nice!";
}
default {
    println "Oh, no...";
};
```

繰り返し
---
この言語では``while``という繰り返し機能を提供しています。

``while``では、条件がtrueもしくは1の間中の処理を繰り返すことができます。以下が基本的な形です。

```
while 条件文 {
    実行内容;
    ...
};
```

以下は、変数``x``が5になるまで。つまり5回``while``内の内容を繰り返すというプログラムの例です。
```
x = value 1;

while #(x <= 5) {
    println "Hello!";
    x = add 1;
};
```

**配列の繰り返し**機能としては、``each``という機能を提供しています。``each``は、配列の要素を1つずつ取り出して繰り返し処理を行うことができます。

``each``の書き方は以下の通りです。
```
each 配列名or配列 > 変数名 {
    実行内容;
    ...
};
```
``変数名``には、配列の要素を1つずつ取り出したときにその値を入れる変数名を指定します。その変数は自動的に作成され、そのブロック内でのみ有効です。

以下は、配列``arr``の要素を1つずつ取り出して出力するプログラムの例です。
```
arr = array ("Good" "Nice" "Great");
each arr > i {
    printf ":1:!\n" i;
};
```
これを実行すると、``Good!``、``Nice!``、``Great!``の3つが出力されます。

配列のインデックスを取得する時は、``>``のところを``:``に変えることで可能です。以下はその例です。
```
arr = array ("Good" "Nice" "Great");
each arr : i {
    printf ":1:\n" i;
};
```
これを実行すると``arr``のインデックス、つまり``0``、``1``、``2``が出力されます。

辞書型の繰り返しも提供しています。その場合の``each``の書き方は以下の通りです。
```
each 辞書 > (キー 値) {
    実行内容;
    ...
};
```
``each``のブロック内で、``キー``と``値``の2つの変数を指定することで、辞書のキーと値を取り出すことができます。

以下は、辞書``dict``のキーと値を取り出して出力するプログラムの例です。
```
dict = {"key1" : "value1" "key2" : "value2"};
each dict > (key value) {
    printf ":1: :2:\n" key value;
};
```
これを実行すると、``key1 value1``、``key2 value2``の2つが出力されます。

また、繰り返し処理を中断したりやり直したりするために、``break``・``continue``関数が提供されています。

``break``関数は以下のように書き、実行すると繰り返し処理を中断して繰り返し処理のループから抜けます。
```
break "";
```

``continue``関数は以下のように書き、実行すると繰り返し処理を始めに戻してやり直します。
```
continue "";
```

関数
---
この言語では関数の宣言として``fnc``を使用します。関数を宣言することで様々な処理を1かたまりにして、何度でも呼び出すことができます。

関数の宣言は、以下のように書きます。
```
fnc 関数名 (引数1 引数2 ...(任意)) {
    関数の実行内容;
    ...
};
```

関数の呼び出しは、以下のように書きます。
```
関数名 (引数1 引数2 ... (ある場合は書く));
```

また返り値（``return``）がある場合は、``to 変数名``を引数を追加します。
```
関数名 (引数) to 返り値を入れる変数名;
```

関数で、返り値を返すには``return 値;``を使用します。それが実行されると返り値を返して関数の処理を終了します。

以下は関数を用いて人の名前を入力して挨拶の文言を出力するプログラムの例です。
```
fnc say_hello ($name) {
    printf "Hello, :1:!\n" $name;
};

say_hello ("Tanaka");
say_hello ("Yamada");
// -> 「Hello, Tanaka!」と「Hello, Yamada!」の2つが出力される。;
```

以下は関数を用いて2数の足し算の和を返すプログラムの例です。
```
fnc add ($x $y) {
    return #($x + $y);
};

make var result;
add (1 2) to result;

println result;
// -> 「3」と出力される;
```

その他の関数について
---
基本的な関数や機能について説明しました。
その他の関数については以下のページを参考にしてください
→ [標準関数一覧](./common-function.md)

サンプルコード
---
様々なサンプルコードを用意しています。ぜひ参考にしてください。
→ [サンプルコード](./sample-code.md)